<%
'// 롯데아이몰API 연동서버 URL
Dim ltiMallAPIURL, ltiMallAuthNo, ltiMallTenID, tenBrandCd, tenDlvCd, tenDlvFreeCd
IF application("Svr_Info") = "Dev" THEN
	'ltiMallAPIURL = "http://openapidev.lotteimall.com"	'' 테스트서버
	ltiMallAPIURL = "http://openapitst.lotteimall.com"	'' 테스트서버
	tenDlvCd = "26645"
	tenDlvFreeCd = "577045"
Else
	ltiMallAPIURL = "https://openapi.lotteimall.com"		'' 실서버
	tenDlvCd = "23725" 
	tenDlvFreeCd = "577045"
End if
ltiMallTenID = "011799LT"
'// 롯데아이몰 인증코드 확인(매일 업데이트; 어플리케이션변수에 저장)
'Application("ltiMallAuthDate")="2012-01-01"
If Application("ltiMallAuthDate") = "" or Datediff("d", Application("ltiMallAuthDate"), date()) > 0 Then
	Dim objXML, xmlDOM
	Set objXML= CreateObject("MSXML2.ServerXMLHTTP.3.0")
	    objXML.Open "GET", ltiMallAPIURL & "/openapi/createCertification.lotte?strUserId=" & ltiMallTenID & "&strPassWd=cube101010!*", False
		objXML.setRequestHeader "Content-Type", "application/x-www-form-urlencoded"
		objXML.Send()
		If objXML.Status = "200" Then
			Set xmlDOM = Server.CreateObject("MSXML2.DomDocument.3.0")
			xmlDOM.async = False
			xmlDOM.LoadXML BinaryToText(objXML.ResponseBody, "euc-kr")
			On Error Resume Next
				Application("ltiMallAuthNo") = xmlDOM.getElementsByTagName("SubscriptionId").item(0).text		'인증번호 저장
				If Application("ltiMallAuthNo") = "" Then														'인증 실패됐을 경우
					Response.Write "<script language=javascript>alert('Lotteimall.com인증코드 전송에러가 발생하였습니다.\n나중에 다시 시도해보세요');history.back();</script>"
					Response.End					
				End If

				Application("ltiMallAuthDate") = now()															'인증시간 기록
				If Err <> 0 then
					Response.Write "<script language=javascript>alert('Lotteimall.com인증에 오류가 발생했습니다.\n나중에 다시 시도해보세요');history.back();</script>"
					Response.End
				End If
			On Error Goto 0
			Set xmlDOM = Nothing
		Else
			Response.Write "<script language=javascript>alert('Lotteimall.com과 통신중에 오류가 발생했습니다.\n나중에 다시 시도해보세요');history.back();</script>"
			Response.End
		End If
	Set objXML = Nothing
End If
ltiMallAuthNo = Application("ltiMallAuthNo")
%>